!@encoding CP1252
model ModelName
uses "mmxprs"; !gain access to the Xpress-Optimizer solver

declarations
	p = 1..100
	dst = 1..3
	qb = 4..28
	rb = 29..59
	te = 60..66
	wr = 67..100
	
	x: array(p) of mpvar
	y: array(p) of mpvar
	Salary: array(p) of integer
	Score: array(p) of integer
	z: array(p) of mpvar
end-declarations

initializations from 'Salary.txt'
	Salary
end-initializations

initializations from 'Score.txt'
	Score
end-initializations

forall(i in p) do
	x(i) is_binary
	y(i) is_binary
	z(i) is_binary
end-do

Obj:= 84001*(151*sum(i in p) x(i)*Score(i) + sum(i in p) y(i)*Score(i)) - sum(i in p) y(i)*Salary(i)

sum(i in p) x(i) = 6
sum(i in p) x(i)*Salary(i) <= 50000
sum(i in dst) x(i) = 1
sum(i in qb) x(i) = 1
sum(i in rb) x(i) >= 1
sum(i in te) x(i) >= 1
sum(i in wr) x(i) >= 1
sum(i in rb) x(i) <= 2
sum(i in te) x(i) <= 2
sum(i in wr) x(i) <= 2

sum(i in p) y(i) = 6
sum(i in p) y(i)*Salary(i) <= 50000
sum(i in dst) y(i) = 1
sum(i in qb) y(i) = 1
sum(i in rb) y(i) >= 1
sum(i in te) y(i) >= 1
sum(i in wr) y(i) >= 1
sum(i in rb) y(i) <= 2
sum(i in te) y(i) <= 2
sum(i in wr) y(i) <= 2

forall(i in p) do
	z(i) >= x(i) + y(i) - 1
end-do

sum(i in p) z(i) <= 0

! Diversified in position
sum(i in rb) (x(i) + y(i)) <= 3
sum(i in te) (x(i) + y(i)) <= 3
sum(i in wr) (x(i) + y(i)) <= 3

maximize(Obj)

writeln("Begin running model")

forall(i in p | getsol(x(i)) > 0) do
	writeln("x", i, " = ", getsol(x(i)))
end-do

forall(i in p | getsol(y(i)) > 0) do
	writeln("y", i, " = ", getsol(y(i)))
end-do

writeln("First team obj: ", getsol(sum(i in p) x(i)*Score(i)))
writeln("First team cost: ", getsol(sum(i in p) x(i)*Salary(i)))
writeln("Second team obj: ", getsol(sum(i in p) y(i)*Score(i)))
writeln("Second team cost: ", getsol(sum(i in p) y(i)*Salary(i)))

writeln("End running model")

end-model